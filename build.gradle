plugins {
    id 'java'
}

group = 'com.dragons'
version = '0.0.1-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // Apply Lombok to all submodules for both main and test sources
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'

        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

configure(subprojects) {
    plugins.withId('io.spring.dependency-management') {
        dependencyManagement {
            imports {
                mavenBom "org.testcontainers:testcontainers-bom:2.0.1"
            }
        }
    }
}
